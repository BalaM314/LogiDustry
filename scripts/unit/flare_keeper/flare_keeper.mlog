# Switch
sensor enabled switch1 @enabled
jump disable_skip equal enabled 1
    print "[sky]Flare Keeper v1.2.1[] by [green]SBytes\n\n[magenta] Disabled"
    printflush message1
    end
disable_skip:


# Bind Unit
ubind @flare

# Unit dead?
sensor uDead unit @dead
jump printUnits equal uDead 1


# Print results
jump print_skip notEqual unit @unit
    printUnits:
        print "[sky]Flare Keeper v1.2.1[] by [green]SBytes[]\n[lightgray]Reduced rally radius for more safety.[]\n\n"
        jump skipPrintInit notEqual prevTime 0
            print "Initializing Timer..."
            set prevTime @time
            jump printSlowProc_skip always
        skipPrintInit:
        print " Count: "
        print uCount
        print "\n Deflagged: "
        print uDCount
        print " ("
        print uDTCount
        print ")"
        op sub timeTaken @time prevTime
        set prevTime @time
        op lessThanEq printSlowProc_skip timeTaken 1e4
        jump printSlowProc_skip equal printSlowProc_skip 1
            print "\n\n[red] Processor too slow![]\n    "
            op div timeTakenF timeTaken 1e1
            op floor timeTakenF timeTakenF
            op div timeTakenF timeTakenF 1e2
            print timeTakenF
            print "s > 10s\n[gray]Copy code into faster processor and link switch and message"
        printSlowProc_skip:
        printflush message1
        set uCount 0
        op add uDTCount uDTCount uDCount
        set uDCount 0
        set unit @unit
print_skip:

# Ctrl check
sensor flag @unit @flag
jump skip_check equal flag 0
sensor ctrl @unit @controlled
jump 0 equal ctrl 1
jump deflag notEqual flag 0
skip_check:

# Add counter
op add uCount uCount 1

# Locate core
ulocate building core 0 0 cx cy null core

# Drop items then approach core 20 rad
sensor item @unit @firstItem
jump dropItem notEqual item 0
    ucontrol approach cx cy 6
    end

dropItem:
    ucontrol itemDrop core 999
    ucontrol move cx cy
end

# Deflag
deflag:
    jump 0 equal ctrl 1
    op add uDCount uDCount 1
    ucontrol flag 0
    # Also override movement
    ulocate building core 0 0 cx cy null core
    ucontrol move cx cy
